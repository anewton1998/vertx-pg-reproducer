import org.flywaydb.gradle.task.*

buildscript {
    ext.kotlin_version = '1.2.60'

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.postgresql:postgresql:42.1.4'
    }
}

plugins {
    id "org.flywaydb.flyway" version "5.1.4"
    id 'java'
}

group = 'com.rcode3'
description ="""
Vert.x PG Replicator Reproducer:${project.name}"""

apply plugin: 'kotlin'
apply plugin: 'application'

sourceCompatibility = 1.8

/**
 * This is used by the gradle run task to start Vertx.
 * This points to the Vertx Launcher.
 */
mainClassName = "$vertx_launcher"

run {
    args "run", "$repl_main_verticle", "-conf" , "src/test/resources/config.json"
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "io.github.microutils:kotlin-logging:1.5.4"
    compile "org.slf4j:slf4j-jdk14:1.7.25"
    compile "io.vertx:vertx-core:$vertx_version"
    compile "io.vertx:vertx-lang-kotlin:$vertx_version"
    compile "io.vertx:vertx-rx-java2:$vertx_version"
    //compile "io.reactiverse:reactive-pg-client:0.10.1"
    compile "io.reactiverse:reactive-pg-client:0.10.2-SNAPSHOT"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.+"
    testCompile "io.vertx:vertx-junit5:$vertx_version"
    testCompile "org.assertj:assertj-core:3.8.0"
    // the jupiter-engine version needs to be kept in line with the vertx-junit5 dependency on junit
    testRuntime "org.junit.jupiter:junit-jupiter-engine:5.0.3"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {

        /**
         * Use the built-in Vertx Launcher to start vertx, this points to the main class.
         */
        attributes 'Main-Class': "$vertx_launcher"

        /**
         * This points the Vertx Launcher at our Main verticle.
         */
        attributes 'Main-Verticle': "$repl_main_verticle"
    }

    /**
     *  Create a fat jar
     */
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree( it ) }
    }
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
    }
}

flyway {
    url = 'jdbc:postgresql://localhost:5433/postgres'
    user = 'postgres'
    password = 'password'
    schemas = [ 'public' ]
}

task srcDbInit(type: FlywayMigrateTask) {
    url = 'jdbc:postgresql://localhost:5432/postgres'
    user = 'postgres'
    password = 'password'
    schemas = [ 'public' ]
    baselineOnMigrate = true
    locations = ['filesystem:src/main/resources/srcdb/migration']
}

